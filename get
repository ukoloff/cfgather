#!/usr/bin/perl

use strict;
use DBI;
use Expect;
use FindBin;
use Digest::SHA1 qw(sha1_hex);

my $h=DBI->connect("DBI:mysql::mysql_read_default_file=$FindBin::Bin/.my.cnf");

$h->do('Insert Into `log`() Values()');
my $logID=$h->{'mysql_insertid'};

my $ins=$h->prepare("Insert Into config(`log`, device, info) values($logID, ?, ?)");

my $s=$h->prepare(<<SQL);
Select device.*, model.ANSI, snippet.command,
    sha1((Select info From config Where device=device.id Order By ctime Desc Limit 1)) As sha1
From device, model, snippet
Where Not disable And itime is Not Null
And device.model=model.id
And device.model=snippet.model And snippet.name='config'
SQL
$s->execute;
#$s->dump_results; exit;
while(my $r=$s->fetchrow_hashref)
{
 print $r->{name}, "\t[", $r->{host}, "]\n"	if -t;

 my $z=new Expect;
 $z->slave->stty(qw(-echo));
 $z->raw_pty(1);
 $z->log_stdout(0);
 $z->restart_timeout_upon_receive(1);
 $z->spawn(qw(/usr/bin/plink -batch -i), "$FindBin::Bin/.ssh/cfgather.ppk", "cfgather\@$r->{host}");
 $z->send($r->{command}."\n");
 $z->expect(10);
 my $S=$z->before;
 undef $z;

 $S=~s/\e\[.*?[a-z]//ig		if $r->{ANSI};	# Clear ANSI terminal escapes
 $S=~s|(?s).*(?m-s)<Configuration>.*$||;
 $S=~s|(?m)^.*(?s-m)</Configuration>.*||;
 $S=~s/^\s+//;
 $S=~s/\s+$//;
 $S.="\n";

 next	if sha1_hex($S) eq $r->{sha1};

 $ins->execute($r->{id}, $S);
}
__END__
